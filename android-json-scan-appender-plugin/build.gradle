plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group rootProject.group
version rootProject.version

repositories {
    mavenCentral()
    google()
    mavenLocal()
}

dependencies {
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    implementation 'com.android.tools.build:gradle:4.2.2'
    implementation project(':tinydi')
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        appendStaticJsonScanForAndroid {
            id = 'tinydi.android-json-scan-appender-plugin'
            implementationClass = 'com.github.zhgzhg.tinydi.build.android.JsonScanArtifactAppenderToBuildPlugin'
        }
    }
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.group + '.' + project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': project.group + '.' + project.name
        )
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/zhgzhg/TinyDI")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        AdroidJsonScanAppenderPlugin(MavenPublication) {
            from(components.java)
            artifactId 'android-json-scan-appender-plugin'
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/zhgzhg/TinyDI.git'
                    developerConnection = 'scm:git:ssh://github.com/zhgzhg/TinyDI.git'
                    url = 'https://github.com/zhgzhg/TinyDI'
                }
            }
        }
    }
}
