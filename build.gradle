plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group 'com.github.zhgzhg.tinydi'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation 'io.github.classgraph:classgraph:4.8.110'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.enabled true
    }
}

jacoco {
    toolVersion = "0.8.7"
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.group + '.' + project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': project.group + '.' + project.name
        )
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/zhgzhg/TinyDI")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        TinyDI(MavenPublication) {
            from(components.java)
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/zhgzhg/TinyDI.git'
                    developerConnection = 'scm:git:ssh://github.com/zhgzhg/TinyDI.git'
                    url = 'https://github.com/zhgzhg/TinyDI'
                }
            }
        }
        gpr(MavenPublication) {
            from(components.java)
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/zhgzhg/TinyDI.git'
                    developerConnection = 'scm:git:ssh://github.com/zhgzhg/TinyDI.git'
                    url = 'https://github.com/zhgzhg/TinyDI'
                }
            }
        }
    }
}